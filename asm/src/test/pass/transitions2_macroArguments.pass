Process Transitions2_MacroArguments {
    Data {
        "agents" -> {
            "A" -> {"foo"},
            "B" -> {"bar", "baz"}
        }
    }

    Subject A {
        StartSubject := true
        InputPool := 2

        Macro Main {
            START: "SelectAgents" ("b1", "B", 1, 1) -> load2
            load2: "SelectAgents" ("b2", "B", 1, 1) -> call1

            call1: "CallMacro"("Ping", "b1") {
              "success" -> call2
              "fail" -> TERMINATE_Fail_Send1
            }

            call2: "CallMacro"("Ping", "b2") {
              "success" -> receive
              "fail" -> TERMINATE_Fail_Send2
            }

            receive: Receive {
                ["pong" from 2 of B] (auto) -> TERMINATE
                (timeout (5)) -> TERMINATE_Fail_Receive
            }

            TERMINATE_Fail_Send1:   Terminate ("timeout: failed to send #1")
            TERMINATE_Fail_Send2:   Terminate ("timeout: failed to send #2")
            TERMINATE_Fail_Receive: Terminate ("timeout: no reply")
        }

        Macro Ping ("receivers") {
            START: Send {
                ["ping" to B in "receivers" with content of "$empty"] -> TERMINATE_Success
                (timeout (0)) -> TERMINATE_Fail
            }

            TERMINATE_Success: Terminate ("success")
            TERMINATE_Fail:    Terminate ("fail")
        }
    }

    Subject B {
        InputPool := 1

        Macro Main {
            START: Receive {
                ["ping" from A store message in "tmp"] (auto) -> reply
                (timeout (0)) -> TERMINATE_Fail1
            }

            reply: Send {
                ["pong" to A in "tmp" with content of "tmp"] -> TERMINATE
                (timeout (0)) -> TERMINATE_Fail2
            }

            TERMINATE_Fail1: Terminate ("timeout1")
            TERMINATE_Fail2: Terminate ("timeout2")
        }
    }
}
