Process Transitions2_Cancel {
    Data {
        "agents" -> {
            "A" -> {"foo"}
        }
    }

    Subject A {
        StartSubject := true

        Macro Main {
            START: "ModalSplit" {
                "a" -> Receive1
                "b" -> Cancel1
            }


            Receive1: Receive {
                ["x" from B]   -> TERMINATE_Fail2
                (timeout (10)) -> TERMINATE_Fail1 // 10 seconds until it should be canceled by Cancel3
                (cancel)       -> Receive2
            }

            Receive2: Receive {
                ["x" from B]  -> TERMINATE_Fail2
                (timeout (1)) -> TERMINATE_Fail3
                (cancel)      -> Join
            }


            // ensure Receive1 is waiting
            Cancel1: InternalAction {
                (timeout (1)) -> Cancel2
            }

            // Receive2 is not yet active, therefore this must timeout
            Cancel2: "Cancel" {
                "Receive2" (auto) -> TERMINATE_Fail4
                (timeout (1))     -> Cancel3
            }

            // Receive1 is still waiting, can be canceled now
            Cancel3: "Cancel" {
                "Receive1" (auto) -> Cancel4
                (timeout (2))     -> TERMINATE_Fail5
            }

            // Receive2 should become active
            Cancel4: "Cancel" {
                "Receive2" (auto) -> Join
                (timeout (5))     -> TERMINATE_Fail6
            }

            Join: "ModalJoin" -> TERMINATE

            TERMINATE_Fail1: Terminate ("Receive1 was not deactivated")
            TERMINATE_Fail2: Terminate ("nothing should have been received")
            TERMINATE_Fail3: Terminate ("Receive2 was not deactivated")
            TERMINATE_Fail4: Terminate ("cancel should not have canceled an inactive state")
            TERMINATE_Fail5: Terminate ("cancel timed out")
            TERMINATE_Fail6: Terminate ("cancel should have canceled Receive2")
        }
    }

    Subject B {
        Macro Main {
            START: InternalAction -> TERMINATE
        }
    }
}
